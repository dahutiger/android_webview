apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        multiDexEnabled true
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    signingConfigs {
        release {
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
//            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles file('./proguard').listFiles()
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles file('./proguard').listFiles()
        }

        forTest {
            initWith(buildTypes.release)

            debuggable true
            matchingFallbacks = ['release']
            versionNameSuffix "-${getCommitId()}"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}


static def getCommitId() {
    return "git rev-parse --short HEAD".execute().text.trim()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])

    // Support库
    implementation rootProject.ext.supportLibs
    //GlideOkHttp
//    implementation(rootProject.ext.glideOkhttp) {
//        exclude group: 'glide-parent'
//    }

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.okhttp3:okhttp:3.14.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.1'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    implementation 'com.uber.autodispose:autodispose:1.3.0'
    implementation 'com.uber.autodispose:autodispose-android:1.3.0'
    implementation 'com.uber.autodispose:autodispose-android-archcomponents:1.3.0'

    // 状态栏颜色修改
    implementation 'com.gyf.immersionbar:immersionbar:3.0.0-beta03'
}
